#!/bin/csh

# DESCRIPTION: I use the script to transfer my local ports and/or
# patches or work in progress ports to my own ports overlay on GitHub.
# DEPENDENCIES: rsync, ports tree
# AUTHOR: nxjoseph

set id=`id -u`
set portsdir="/usr/ports"
set maintainer="nxjoseph@protonmail.com"
set pureports="$portsdir/pure-ports"
set portsf="$portsdir/ports"
set destdir="/home/yusuf/Documents/yusuf-ports-overlay"

if ( "$id" != 0 ) then
	echo "please run this as root"
	exit 1
endif

if ( -d "$portsdir" ) then
	cd "$portsdir"
	set curdir=`pwd`
	if ( "$curdir" == "$portsdir" ) then
		find . -type f -mindepth 3 -maxdepth 3 -iname makefile -exec grep -i "maintainer.*$maintainer" {} + >& "$portsf"
		cat "$portsf" | awk -F'/' '{print $2"/"$3}' | awk -F':' '{print $1}' > & "$pureports"
		foreach dir (`cat $pureports`)
   		set first_dir = `echo "$dir" | cut -d'/' -f1`
   		set destination_path = "$destdir/$first_dir"
  		if ( ! -d "$destination_path" ) then 	
			mkdir -p "$destination_path"
			if ( "$status" != 0 ) then
				echo "could not create $destination_path"
				exit 1
			endif
		endif
   		rsync -au "$dir" "$destination_path"
		if ( "$status" != 0 ) then
			echo "Couldn't copy $dir to $destination_path"
			exit 1
		endif
   		
   		echo "Copied $dir to $destination_path"
		end		
	endif
else
	echo "could not found ports tree"
	exit 1
endif

if ( -f "$pureports" && -f "$portsf") then
	rm -f "$pureports" "$portsf"
else
	echo "$pureports & $portsf not found"
	exit 1
endif

exit 0
