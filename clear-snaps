#!/bin/csh

# DESCRIPTION: I use the script to only keep a specified snapshot
# and destroy rest the other ones. It also re-crates poudriere jails'
# 'clean' snapshots because they are required for poudriere to work.
# DEPENDENCIES: grep, zfs, poudriere (optional)
# AUTHOR: nxjoseph

set jailpool="zroot"

echo "Pool to operate on: "
set pool = $<
echo "Snapshot to keep: "
set keep = $<

if ( "$pool" == "" || "$keep" == "" ) then
	echo "Required variables aren't defined"
	exit 1
endif

foreach snap ( `zfs list -H -o name -t snap -r $pool | grep -vxE ".*$keep"` )
	if ( "$status" != 0 ) then
		echo "Error finding snapshot"
		exit 1
	endif
	zfs destroy -v $snap
	if ( "$status" != 0 ) then
		echo "Error destroying snapshot $snap"
		exit 1
	endif
end

if ( "$pool" == "$jailpool" ) then
	set clean=`zfs list -t snap -r $pool | grep clean`
	if ( "$status" != 0 ) then
		echo "Clean jails not found, re-creating them..."
		foreach jail ( `zfs list -H -o name -r $pool/poudriere/jails | sed '1d' | cut -c 23-` )
			if ( "$status" != 0 ) then
				echo "Error creating clean jail $jail"
				exit 1
			endif
			zfs snapshot $pool/poudriere/jails/$jail@clean
			if ( "$status" != 0 ) then
				echo "Error creating snapshot $jail"
				exit 1
			endif
		end
	else
		echo "Clean jails already found, not creating them."
	endif
endif

exit 0
